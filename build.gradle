import java.time.Instant

plugins {
    id 'java'
    id 'application'
    id 'com.palantir.git-version'
    id 'com.coditory.integration-test'
}

ext {
    gradleWrapperVersion = ext.gradleVersion
    generatedResourcesDir = file("${buildDir}/generated-resources")
}

group = 'com.dburyak.exercise'
version gitVersion()
description = 'Bowl game results printer CLI app (Java exercise)'
version gitVersion()
ext { isRelease = versionDetails().isCleanTag }
if (!isRelease) { 
    project.version += '-SNAPSHOT'
}

sourceCompatibility = 11
targetCompatibility = 11

repositories {
    mavenLocal()
    jcenter()
    mavenCentral()
}

sourceSets.each {
    it.resources.srcDirs generatedResourcesDir
}

dependencies {
    // lombok
    compileOnly "org.projectlombok:lombok:${lombokVersion}"
    annotationProcessor "org.projectlombok:lombok:${lombokVersion}"
    testCompileOnly "org.projectlombok:lombok:${lombokVersion}"
    testAnnotationProcessor "org.projectlombok:lombok:${lombokVersion}"
    integrationCompileOnly "org.projectlombok:lombok:${lombokVersion}"
    integrationAnnotationProcessor "org.projectlombok:lombok:${lombokVersion}"

    // testing (junit 5 + assertj)
    testImplementation "org.junit.jupiter:junit-jupiter-engine:${junitVersion}"
    testImplementation "org.assertj:assertj-core:${assertjVersion}"

    // cli
    implementation "info.picocli:picocli:${picocliVersion}"
}

defaultTasks 'assembleDist'

wrapper {
    gradleVersion = gradleWrapperVersion
    distributionType = 'ALL'
}

tasks.withType(Test) {
    useJUnitPlatform()
}

jar {
    archiveBaseName = project.name
}

application {
    mainClass = 'com.dburyak.exercise.game.bowling.BowlingCliApp'
}

task generateVersionInfo() {
    def projectVersion = project.version
    def gitRevision = versionDetails().gitHashFull
    def outFiles = fileTree(generatedResourcesDir)
    inputs.property('version', projectVersion)
    inputs.property('revision', gitRevision)
    outputs.files(outFiles)
    doLast {
        generatedResourcesDir.mkdirs()
        file("${generatedResourcesDir}/version.txt").text = projectVersion
        file("${generatedResourcesDir}/revision.txt").text = gitRevision
        file("${generatedResourcesDir}/built_at.txt").text = Instant.now() as String
    }
}

tasks.withType(ProcessResources) {
    dependsOn generateVersionInfo
}
